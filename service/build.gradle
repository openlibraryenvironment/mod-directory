buildscript {
  repositories {
    jcenter()
    maven { url "https://repo.grails.org/grails/core" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
    classpath "org.grails.plugins:views-gradle:2.3.2"
    classpath "org.grails.plugins:hibernate5:7.3.0"
    classpath 'org.grails.plugins:database-migration:4.2.0'
    classpath 'com.bmuschko:gradle-docker-plugin:6.6.1'
  }
}




final String version_regex = /^(((\d+)\.\d+)\.\d+)(-([a-zA-Z]+(\.\d+)?))?$/
Map<String, String> env = System.getenv();
String calculated_version = null;

if ( ( appVersion.contains('SNAPSHOT') ) && ( env.BUILD_NUMBER != null ) ) {
  println("Detected snapshot version and build number - appending");
  calculated_version = "${appVersion}.${env.BUILD_NUMBER}".toString()
}
else {
  println("Release version");
  calculated_version = "${appVersion}".toString()
}

version "${calculated_version}"
group "org.olf"

apply plugin: 'idea'
apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.plugins.views-json'
apply plugin: 'com.bmuschko.docker-remote-api'

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
if (DefaultNativePlatform.getCurrentOperatingSystem().isWindows()) {
	grails { pathingJar = true }
}

repositories {
  mavenCentral()
  maven { url 'https://repo.grails.org/grails/core' }
  maven { url 'https://maven.indexdata.com/' }
  maven { url "https://maven.k-int.com/repository/releases" }
  maven { url "https://repo.gradle.org/gradle/libs-releases-local" }
}

sourceSets {
  main {
    resources {
      srcDir 'grails-app/migrations'
      srcDir 'src/main/okapi/tenant'
    }
  }
}

dependencies {
  /* ---- Grails 5 ---- */
  compileOnly "io.micronaut:micronaut-inject-groovy"
  console "org.grails:grails-console"
  implementation "org.springframework.boot:spring-boot-starter-logging"
  implementation "org.springframework.boot:spring-boot-autoconfigure"
  implementation "org.grails:grails-core"
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.grails:grails-plugin-url-mappings"
  implementation "org.grails:grails-plugin-rest"
  implementation "org.grails:grails-plugin-codecs"
  implementation "org.grails:grails-plugin-interceptors"
  implementation "org.grails:grails-plugin-services"
  implementation "org.grails:grails-plugin-datasource"
  implementation "org.grails:grails-plugin-databinding"
  implementation "org.grails:grails-web-boot"
  implementation "org.grails:grails-logging"
  implementation "org.grails.plugins:async"
  implementation "org.grails.plugins:hibernate5"
  implementation "org.hibernate:hibernate-core:5.6.11.Final"
  implementation "org.grails.plugins:events"
  implementation "org.grails.plugins:views-json"
  implementation "org.grails.plugins:views-json-templates"
  runtimeOnly "javax.xml.bind:jaxb-api:2.3.1"

  profile "org.grails.profiles:rest-api"

  implementation ('org.olf.reshare.dm.directory:dm-directory:2.1.2') {
    exclude group: 'com.k_int.grails', module: 'web-toolkit-ce'
  }



  /************** Testing ************/
  testImplementation "org.grails:grails-gorm-testing-support"
  testImplementation "org.mockito:mockito-core"
  testImplementation "org.grails:grails-web-testing-support"
  testImplementation "org.grails.plugins:geb"
  implementation 'org.z3950.zing:cql-java:1.13'


  implementation 'org.grails.plugins:spring-security-core:5.2.1'
  implementation "org.springframework.boot:spring-boot-starter-undertow" // Replaces spring-boot-starter-tomcat
  implementation "org.hibernate:hibernate-core:5.4.19.Final"             // Update to latest 5.4
  implementation "org.hibernate:hibernate-java8:5.4.19.Final"
  runtimeOnly "com.zaxxer:HikariCP:5.0.1"
  runtimeOnly "org.postgresql:postgresql:42.2.23"

  implementation ('org.grails.plugins:database-migration:4.2.0') {
    exclude group: 'org.liquibase', module: 'liquibase-core'
    exclude group: 'com.vaadin.external.google', module: 'android-json'
  }
  implementation 'org.liquibase:liquibase-core:4.19.1'

  implementation 'com.opencsv:opencsv:4.6'
  implementation 'commons-io:commons-io:2.7'

//  implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'     // TODO: Migrate away from this resource.
  implementation 'io.github.http-builder-ng:http-builder-ng-core:1.0.4'

  // K-int
  implementation 'com.k_int.grails:web-toolkit-ce:8.1.0'
  implementation ('com.k_int.okapi:grails-okapi:6.0.0-rc.4') {
    exclude group: 'com.vaadin.external.google', module: 'android-json'
  }

  // Custom non profile deps.
  implementation 'org.apache.kafka:kafka-clients:2.3.0'

  // Better test reports.
  testImplementation( 'com.athaydes:spock-reports:2.3.2-groovy-3.0' ) {
    transitive = false // this avoids affecting your version of Groovy/Spock
  }
}

bootRun {
  ignoreExitValue true
  jvmArgs(
      '-Dspring.output.ansi.enabled=always',
      '-server',
      '-noverify',
      '-XX:TieredStopAtLevel=1',
      '-Xmx1024m')
  sourceResources sourceSets.main
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
}

task generateDescriptors(type: Copy) {

  dependsOn buildProperties

  File grailsBuildInfoFile = buildProperties.outputs.files.files.find { it.name == 'grails.build.info' }

  final def props = [:]

  if(!(grailsBuildInfoFile)) return // No need to continue if the file is not there

  doFirst {

    // Place the loading within a do first as the info file from the buildProperties
    // task will not be present until it is executed. This owuld cause a file not found
    // during task configureation otherwise.
    Properties properties = new Properties()

    // Read properties from the file
    grailsBuildInfoFile.withInputStream {
      properties.load(it)
    }

    // Need to parse multi-dimensional keys.
    ConfigSlurper cs = new ConfigSlurper()
    props.putAll(cs.parse(properties))
  }

  from ('./src/main/okapi') {
    include '*-template.json'
  }
  into './build/resources/main/okapi'
  filteringCharset = 'UTF-8'
  expand(props)
  rename { String fileName ->
    fileName.replace('-template', '')
  }
}

jar {
  version "${version.replaceAll(version_regex, '$2')}"
}

buildProperties.doLast {

  // Find the right file
  File grailsBuildInfoFile = it.outputs.files.files.find { it.name == 'grails.build.info' }

  if(!grailsBuildInfoFile) {
    System.err.println("Unable to locate grails.build.info");
    return // No need to continue if the file is not there
  }

  Properties properties = new Properties()

  // Read properties from the file
  grailsBuildInfoFile.withInputStream {
    properties.load(it)
  }

  // Load the app version
  String version = properties.getProperty('info.app.version')
  def versionParts
  if (version && (versionParts = version =~ version_regex)) {
    println("Version parts : ${versionParts[0]}");
    versionParts[0].with {
      properties.setProperty('info.app.majorVersion', it[3])
      properties.setProperty('info.app.minorVersion', it[2])
      properties.setProperty('info.app.patchVersion', it[1])

      if (it[5]) {
        properties.setProperty('info.app.testVersion', it[5])
      }

      println("version properties: ${properties}");
    }

    version.replaceAll(/^(((\d+)\.\d+)\.\d+)(-([\w]+))?$/ , '$1')
  }

  if(env.BUILD_NUMBER)
    properties.setProperty('build.number', env.BUILD_NUMBER)

  if ( env.GIT_REVISION )
    properties.setProperty('build.git.revision', env.GIT_REVISION)

  if ( env.GIT_BRANCH )
    properties.setProperty('build.git.branch', env.GIT_BRANCH)

  if (env.GIT_COMMIT)
    properties.setProperty('build.git.commit', env.GIT_COMMIT)

  // Add new properties from various sources
  properties.setProperty('build.time', new Date().format("yyyy-MM-dd HH:mm:ss"))

  properties.setProperty('build.host', InetAddress.localHost.hostName)

  // Get a System property
  properties.setProperty('build.java.version', System.getProperty('java.version'))

  // Write the properties back to the file
  grailsBuildInfoFile.withOutputStream {
    properties.store(it,null)
  }
}

processResources.dependsOn generateDescriptors

/**
 * Docker build config.
 */
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

ext {
  dockerTag = "${project.dockerRepo}/${project.name}:${project.version}${dockerTagSuffix}"
  dockerBuildDir = mkdir("${buildDir}/docker")
}

task prepareDocker(type: Copy, dependsOn: assemble) {
  description = 'Copy files to build directory'
  group = 'Docker'

  from project.jar

  // from otherfileLoc

  into dockerBuildDir
}

task createDockerfile(type: Dockerfile, dependsOn: prepareDocker) {
  description = 'Create a Dockerfile file'
  group = 'Docker'

  destFile = project.file("${dockerBuildDir}/Dockerfile")

  from 'folioci/alpine-jre-openjdk8:latest'
  instruction 'LABEL maintainer="Knowledge Integration"'

  exposePort 8080

  // Copying straight into the default directory should be enough
  copyFile jar.archiveName, '/usr/verticles/application.jar'
}

task buildImage(type: DockerBuildImage, dependsOn: createDockerfile) {
  description = 'Create Docker image to run the Grails application'
  group = 'Docker'

  inputDir = file(dockerBuildDir)
}


tasks.withType(GroovyCompile) {
  configure(groovyOptions) {
    forkOptions.jvmArgs = ['-Xmx1024m']
  }
}

tasks.withType(Test) {
  systemProperty "geb.env", System.getProperty('geb.env')
  systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
}

